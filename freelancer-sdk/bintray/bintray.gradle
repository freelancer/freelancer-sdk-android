apply plugin: 'com.jfrog.bintray'

version = libraryVersionName

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = System.getenv('BINTRAY_API_USER')
    key = System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = bintrayOrg
        desc = libraryDescription
        websiteUrl = libraryUrl
        issueTrackerUrl = libraryIssuesUrl
        vcsUrl = libraryVcsUrl
        licenses = allLicenses
        publish = toPublishLibrary
        publicDownloadNumbers = toPublicDownloadNumbers
        version {
            released = new Date()
            name = libraryVersionName
            vcsTag = libraryVcsUrl
            desc = libraryDescription
        }
    }
}